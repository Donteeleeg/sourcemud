// Player extensions
// Copyright (C) 2002  Sean Middleditch

// show experience
function playerShowExp(self)
	self << "Experiance: " << self.getExp() << "\n";
end

// show stats
function playerShowStats(self)
	self << "Your statistics: \n";
	for i : STAT_COUNT do
		var stat = self.getEffectiveStat(i);
		var mod = self.getStatModifier(i);

		// name
		self << @indent(2) << getStatName(i) << ': ';

		// descriptor
		self << @indent(14) << getStatColor(stat) << getStatLevel(stat) << CNORMAL;

		// value/+mod
		var plus = '';
		if mod >= 0 then plus = '+'; end
		self << @indent(25) << '(' << stat << '/' << plus << mod << ")\n";
	end
	self.setIndent(0);
end
	
// show all player info
function playerShowInfo(self)
	// basics
	self << "Your status:\n";
	printInfo(self, "Name", self.getName());
	printInfo(self, "Race", capwords(self.getRace().getName()));
	if self.getGender() == GENDER_MALE then
		printInfo(self, "Gender", "Male");
	elif self.getGender() == GENDER_FEMALE then
		printInfo(self, "Gender", "Female");
	else
		printInfo(self, "Gender", "Neuter");
	end

	// age
	printInfo(self, "Age", self.getAge() .. " years (born on " .. self.getBirthday().getDate() .. ")");

	// appearance
	printInfo(self, "Appearance", strParse('{$self.Descend', 'self', self));

	// alignment
	if self.isNeutral() then
		if self.isGood() then
			printInfo(self, "Alignment", "Neutral (with good tendancies)");
		elif self.isEvil() then
			printInfo(self, "Alignment", "Neutral (with evil tendancies)");
		else
			printInfo(self, "Alignment", "Neutral");
		end
	elif self.isGood() then
		printInfo(self, "Alignment", "Good");
	elif self.isEvil() then
		printInfo(self, "Alignment", "Evil");
	else
		printInfo(self, "Alignment", "Unknown");
	end

	self << "\n";
	self.showExp();
end

// helper function
function printInfo(player, label, data)
	player << '  ' << label << ': ' << @indent(14) << data << "\n" << @indent(0);
end

// extend Player
function init()
	Player.addMethod('showExp', playerShowExp);
	Player.addMethod('showStats', playerShowStats);
	Player.addMethod('showInfo', playerShowInfo);
end
